#!/usr/bin/perl

use strict;
use warnings;
use YAML::Tiny;
use Getopt::Long qw(:config posix_default);
use Carp qw(croak);
use List::Util qw(first);
use FindBin '$Bin';
use lib "$Bin/../lib";
use Text::Glob::Expand;
use Cluster::Facts qw(expand_attr_sets expand_node_groups);

######################################################################

sub format_node {
    my ($format, $name, $attrs) = @_;

    my @attr_names;
    $format =~ 
        s{
              %
              (?:
                  %                 # an escaped percent
              |
                  (0)               # %0 stands for the attr set name
              |
                  ( [^\W\d][\w-]* ) # non-digit word char followed by word chars
              |
                  \{ ( [^\}]+ ) \}  # anything except braces
              )
         }
         {
             if (defined $1) {
                 $name;
             }
             elsif (my $name = $2 || $3) {
                 my $val = $attrs->{$name};
                 croak "no such attribute '$name'"
                     unless defined $val;
                 $val;
             }
             else {
                 '%'; # unescape escaped percents
             }
         }gex;

    return $format;
}

######################################################################


my $config_path = first { -f } 
    "$Bin/.node-expand.conf", 
    "$ENV{USER}/.node-expand.conf",
    "/etc/node-expand.conf";

my $node_format = "%0 %login:%password@%0:%port";
GetOptions(
    '--config=s' => \$config_path,
    '--format=s' => \$node_format,
)
    or die "failed to parse options.  stopping.\n";


my $yaml_stream;
if (defined $config_path) {
    if ($config_path eq '-') {
        $yaml_stream = \*STDIN;
    }
    elsif (!-f $config_path) {
        die "No such file '$config_path'. stopping.\n";
    }
    else {
        require IO::File;
        $yaml_stream = IO::File->new($config_path)
            or die "failed to open $config_path: $!";
    }
}
else {
    die "no config file found and none specified with --config.  stopping.\n";
}

my $yaml = do { local $/; <$yaml_stream> };
undef $yaml_stream; # This will close the stream if we opened it

my ($config) = YAML::Tiny::Load($yaml)
    or die "failed to parse config: ", YAML::Tiny->errstr, "\n";


my ($attributes, $groups) = @$config{qw(attributes groups)};


expand_attr_sets($attributes);


my @nodes = expand_node_groups($attributes, $groups, @ARGV);

while(my ($name, $attrs) = splice @nodes, 0, 2) {
    print format_node $node_format, $name, $attrs;
    print "\n";
}

# 
# node-expand yum2 -f "%name%password@%host:%port"
